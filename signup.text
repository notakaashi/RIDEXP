Imports System.Text
Imports System.Security.Cryptography
Imports MySql.Data.MySqlClient
Public Class SIGNUP

    Public connectionString As String = "Server=localhost;Database=ridexp;User=root;Password=;"
    Private Sub SIGNUP_Load(sender As Object, e As EventArgs) Handles MyBase.Load
        FadeTimer.Start()
    End Sub

    Private Sub FadeTimer_Tick(sender As Object, e As EventArgs) Handles FadeTimer.Tick
        If Me.Opacity < 1 Then
            Me.Opacity += 0.05
        Else
            FadeTimer.Stop()
        End If
    End Sub


    Private Sub btnHOME_Click(sender As Object, e As EventArgs)
        Form1.Show()
        Hide()
    End Sub

    Private Sub Label1_Click(sender As Object, e As EventArgs) Handles Label1.Click
        Me.Close()
        Me.Opacity = 0
    End Sub

    Private Sub signupbtn_Click(sender As Object, e As EventArgs) Handles signupbtn.Click
        If String.IsNullOrWhiteSpace(fnametxt.Text) Or
          String.IsNullOrWhiteSpace(lnametxt.Text) Or
          String.IsNullOrWhiteSpace(emailtxt.Text) Or
          String.IsNullOrWhiteSpace(usertxt.Text) Or
          String.IsNullOrWhiteSpace(passwordtxt.Text) Then
            MessageBox.Show("Please fill in all required fields.", "Validation Error", MessageBoxButtons.OK, MessageBoxIcon.Warning)
            Return
        End If

        If InsertUserData() Then
            MessageBox.Show("Sign up successful!", "Success", MessageBoxButtons.OK, MessageBoxIcon.Information)
            ClearForm()
        Else
            MessageBox.Show("Sign up failed. Please try again.", "Error", MessageBoxButtons.OK, MessageBoxIcon.Error)
        End If
    End Sub

    Private Function InsertUserData() As Boolean
        Try
            Using connection As New MySqlConnection(connectionString)
                connection.Open()

                Using transaction As MySqlTransaction = connection.BeginTransaction()
                    Try
                        Dim hashedPassword As String = BitConverter.ToString(SHA256.Create().ComputeHash(Encoding.UTF8.GetBytes(passwordtxt.Text))).Replace("-", "").ToLower()
                        Dim customerQuery As String = "INSERT INTO customers (first_name, last_name, date_of_birth, email, phone, address, license_number, license_expiry) VALUES (@first_name, @last_name, @date_of_birth, @email, @phone, @address, @license_number, @license_expiry)"

                        Using customerCmd As New MySqlCommand(customerQuery, connection, transaction)
                            customerCmd.Parameters.AddWithValue("@first_name", fnametxt.Text)
                            customerCmd.Parameters.AddWithValue("@last_name", lnametxt.Text)
                            customerCmd.Parameters.AddWithValue("@date_of_birth", dobtxt.Text)
                            customerCmd.Parameters.AddWithValue("@email", emailtxt.Text)
                            customerCmd.Parameters.AddWithValue("@phone", phonenumbertxt.Text)
                            customerCmd.Parameters.AddWithValue("@address", addresstxt.Text)
                            customerCmd.Parameters.AddWithValue("@license_number", licensenumbertxt.Text)
                            customerCmd.Parameters.AddWithValue("@license_expiry", licenseexptext.Text)

                            customerCmd.ExecuteNonQuery()
                        End Using


                        Dim userQuery As String = "INSERT INTO user (username, password_hash, role_id) VALUES (@username, @password_hash, @role_id)"

                        Using userCmd As New MySqlCommand(userQuery, connection, transaction)
                            userCmd.Parameters.AddWithValue("@username", usertxt.Text)
                            userCmd.Parameters.AddWithValue("@password_hash", hashedPassword)
                            userCmd.Parameters.AddWithValue("@role_id", 1)

                            userCmd.ExecuteNonQuery()
                        End Using

                        transaction.Commit()
                        Return True

                    Catch ex As Exception
                        transaction.Rollback()
                        MessageBox.Show($"Database error: {ex.Message}", "Error", MessageBoxButtons.OK, MessageBoxIcon.Error)
                        Return False
                    End Try
                End Using
            End Using
        Catch ex As Exception
            MessageBox.Show($"Connection error: {ex.Message}", "Error", MessageBoxButtons.OK, MessageBoxIcon.Error)
            Return False
        End Try
    End Function

    Private Sub ClearForm()
        fnametxt.Clear()
        lnametxt.Clear()
        emailtxt.Clear()
        usertxt.Clear()
        passwordtxt.Clear()
        phonenumbertxt.Clear()
        addresstxt.Clear()
        licensenumbertxt.Clear()
        licenseexptext.Clear()
        dobtxt.Clear()
    End Sub
End Class